
grammar ASTify-grammar



// utilities
Type(Word name, bool optional, bool lst)
: .name .optional('?') .lst('[' ']')

TypedName(Type type, Word name)
: .type .name

MatcherTarget(Word property)
: .property

PatternList(RootPattern[] patterns)
: ':' (@RootPattern -> patterns) .patterns

NamedPropertyList(Word name, TypedName[] properties)
: .name '(' [.properties(',')] ')'

Call(Word functionName, Parameter[] parameters)
: .functionName '(' [.parameters(',')] ')'

Reference(Word reference)
: .reference

union Parameter
: Reference
: Call



// patterns
TypeReference(Word type)
: '@' .type

Terminal(String terminal)
: .terminal

Function(Keyword name, UncapturingPattern[] patterns)
: ('list' -> name) '(' (@UncapturingPattern -> patterns) ')'
: ('delim' -> name) '(' (@UncapturingPattern -> patterns) ',' (@UncapturingPattern -> patterns) ')'

BuiltinPredicate(Word predicateName)
: '{' .predicateName '}'

Matcher(UncapturingPattern source, MatcherTarget targetProperty)
: '(' .source '->' .targetProperty ')'

// wrapper around Matcher
// without a qualifier: (@typeof(property) -> .property)
// with a qualifier (if property is a list): (delim(@typeof(property), qualifier) -> .property)
//                  (if property is an optional): [(qualifier -> .property)]
PropertyReference(Word property, UncapturingPattern[] qualifier)
: '.' .property ['(' .qualifier ')']

Optional(Pattern[] patterns)
: '[' .patterns ']'

Extend(NamedPropertyList properties, PatternList[] patterns, Call call)
: 'extend' .properties ':' (@PatternList -> patterns) 'in' .call

// pattern unions
union Predicate
: BuiltinPredicate

union UncapturingPattern
: TypeReference
: Terminal
: Function
: Predicate

union Pattern
: UncapturingPattern
: Matcher
: PropertyReference

union RootPattern
: Pattern
: Optional



// statements
AbstractTypeDefinition(NamedPropertyList properties)
: 'abstract' .properties

TypeDefinition(NamedPropertyList properties, PatternList[] patternLists)
: .properties .patternLists

Union(Word typename, Word[] subtypes)
: 'union' .typename ':' .subtypes(':')

AliasDefinition(Word name, TypedName? property, PatternList[] patternLists)
: 'alias' .name ['(' .property ')'] (@PatternList -> patternLists) .patternLists

ExternDefinition(Type returnType, Word name, TypedName[] parameters, PatternList[] patternLists)
: 'extern' .returnType .name '(' [.parameters(',')] ')' .patternLists

ApplyStatement(Call call, PatternList[] patternLists)
: 'apply' (@Call -> call) (@PatternList -> patternLists) .patternLists

// definition union
union Definition
: AbstractTypeDefinition
: TypeDefinition
: Union
: AliasDefinition
: ExternDefinition

union Statement
: ApplyStatement
: Definition

// import statements
RelativeImportStatement(Word[] parts)
: 'import' .parts(',')

AbsoluteImportStatement(String importPath)
: 'import' .importPath

union ImportStatement
: RelativeImportStatement
: AbsoluteImportStatement



// grammar
Grammar(Word name)
: 'grammar' .name

ASTify-grammar(ImportStatement[] imports, Grammar? _grammar, Statement[] statements)
: .imports [._grammar] .statements @EOF


